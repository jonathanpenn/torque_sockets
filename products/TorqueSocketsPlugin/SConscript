from Config import *
from Bundle import *
from rez import *

# Import the build environment and static libs from our parent SConstruct.
Import('env')
#Import('libs')

# files stores the files which will be compiled for the NPAPI plugin.
files = Glob('*.cpp')

# MIME types by configuration.  Optimized Debug uses MIME_RELEASE
MIME_RELEASE = 'application/x-torque-socket'
MIME_DEBUG = 'application/x-torque-socket-debug'

# Metadata configuration.  This is for Win32...
hellorc_subst = {}
hellorc_subst['dbg'] = {'FILE_DESC':'nptorque_socket_debug', 'FILE_OPEN_NAME':'nptorque_socket_debug'}
hellorc_subst['dbg'].update({'INTERNAL_NAME':'Torque Socket Debug', 'MIME_TYPE':MIME_DEBUG})
hellorc_subst['dbg'].update({'ORIGINAL_FILE_NAME':'nptorque_socket_debug.dll', 'PRODUCT_NAME':'Torque Socket Plugin Debug'})

hellorc_subst['optdbg'] = {'FILE_DESC':'nptorque_socket_optimized', 'FILE_OPEN_NAME':'nptorque_socket_optimized'}
hellorc_subst['optdbg'].update({'INTERNAL_NAME':'Torque Socket OptDebug', 'MIME_TYPE':MIME_RELEASE})
hellorc_subst['optdbg'].update({'ORIGINAL_FILE_NAME':'nptorque_socket_optimized.dll', 'PRODUCT_NAME':'Torque Socket Plugin OptDebug'})

hellorc_subst['release'] = {'FILE_DESC':'nptorque_socket', 'FILE_OPEN_NAME':'nptorque_socket'}
hellorc_subst['release'].update({'INTERNAL_NAME':'Torque Socket', 'MIME_TYPE':MIME_RELEASE})
hellorc_subst['release'].update({'ORIGINAL_FILE_NAME':'nptorque_socket.dll', 'PRODUCT_NAME':'Torque Socket Plugin'})

hellodef_subst = {}
hellodef_subst['dbg'] = {'LIBNAME':'NPTORQUE_SOCKET_DEBUG'}
hellodef_subst['optdbg'] = {'LIBNAME':'NPTORQUE_SOCKET_OPTIMIZED'}
hellodef_subst['release'] = {'LIBNAME':'NPTORQUE_SOCKET'}

# And this is for OS X...
infoplist_subst = {}
infoplist_subst['dbg'] = {'MIME_TYPE':MIME_DEBUG}
infoplist_subst['optdbg'] = {'MIME_TYPE':MIME_RELEASE}
infoplist_subst['release'] = {'MIME_TYPE':MIME_RELEASE}

hellor_osx_subst = {}
hellor_osx_subst['dbg'] = {'FILE_DESC':'Torque Socket Debug Plugin', 'FILE_NAME':'Torque Socket Debug Plugin'}
hellor_osx_subst['dbg'].update({'MIME_TYPE':MIME_DEBUG, 'MIME_DESC':'Torque Socket Debug'})

hellor_osx_subst['optdbg'] = {'FILE_DESC':'Torque Socket Optimized Plugin', 'FILE_NAME':'Torque Socket Optimized Plugin'}
hellor_osx_subst['optdbg'].update({'MIME_TYPE':MIME_RELEASE, 'MIME_DESC':'Torque Socket'})

hellor_osx_subst['release'] = {'FILE_DESC':'Torque Socket Plugin', 'FILE_NAME':'Torque Socket Plugin'}
hellor_osx_subst['release'].update({'MIME_TYPE':MIME_RELEASE, 'MIME_DESC':'Torque Socket'})

TOOL_SUBST(env)
env.Append(CPPPATH=['../inc', '#/torque_sockets', '#/core', '.'], LIBPATH=['#/lib/libtommath', '#/lib/libtomcrypt'])

# Determine which platform we're on.
isLinux = env['PLATFORM'] == 'posix' or env['PLATFORM'] == 'linux'
isOSX = env['PLATFORM'] == 'darwin'
isWindows = env['PLATFORM'] == 'win32'
cppdefines = []

# Specify our unadorned target name.  Certain platforms may need to change this.
target = 'torque_socket'

# Linux and OS X need the XP_UNIX #define and prcpucfg_unix.h
if isLinux or isOSX:
   cppdefines += ["XP_UNIX", 'MDCPUCFG="\\"prcpucfg_unix.h"\\"']
if isLinux:
	cppdefines += ["OS_LINUX"]
if isOSX:
   cppdefines += ["XP_MACOSX", "OS_MACOSX"]
   env.Append(LINKFLAGS='-framework CoreFoundation')
if isWindows:
   cppdefines += ["WIN32", "OS_WINDOWS", "NOMINMAX", "_WINDOWS", 'MDCPUCFG="\\"prcpucfg_win32.h"\\"']
   # Substitue the proper names into our *.in files for each configuration we
   # are building.
   f = env.Build('SubstInFile', 'torque_socket.def', 'torque_socket.def.in', copy=False, suffix=False, SUBST_DICT_CFG=hellodef_subst)[0]
   files.append(f)
   f = env.Build('SubstInFile', 'torque_socket.rc', 'torque_socket.rc.in', copy=False, suffix=False, SUBST_DICT_CFG=hellorc_subst)[0]
   files.append(f)
   # On windows the plugin name must start with np.
   target = 'nptorque_socket'

# This is the Linux "metadata" configuration.  Linux uses CPP for everything
# instead of a separate config file, so some of this stuff is still hardcoded
# in plugin.cpp. Sorry.
cppdefine_cfg = {}
cppdefine_cfg['release'] = cppdefines + ['MIME_TYPE="\\"%s"\\"' % MIME_RELEASE]
cppdefine_cfg['optdbg'] = cppdefines + ['MIME_TYPE="\\"%s"\\"' % MIME_RELEASE]
cppdefine_cfg['dbg'] = cppdefines + ['MIME_TYPE="\\"%s"\\"' % MIME_DEBUG]

# And this is where we finally build the plugin.
ret = env.Build('LoadableModule', target, files, CPPDEFINES_CFG=cppdefine_cfg)

# On OS X we need to build the resource files and turn the whole thing into a
# bundle.  We build the Info.plist and tnp.r files here because it's simpler
# and keeps the related code together, as neither are inputs into the
# LoadableModule builder (as compaared with the Win32 .def and .rc files)
if isOSX:
   TOOL_BUNDLE(env)
   TOOL_REZ(env)
   f = env.Build('SubstInFile', 'Info.plist', 'Info.plist.in', copy=False, suffix=False, SUBST_DICT_CFG=infoplist_subst)[0]
   res = env.Build('SubstInFile', 'torque_socket.r', 'torque_socket.r.in', copy=False, suffix=False, SUBST_DICT_CFG=hellor_osx_subst)[0]
   rsrc = env.Build('REZ', 'torque_socket', res, copy=False, suffix=True)[0]
   configs = env.subst('${cfg}').split()
   for config in configs:
      config = Config.allConfigs[config]
      env.MakeBundle('torque_socket' + config.suffix + '.plugin', 'torque_socket' + config.suffix,
                     'build/' + config.name + '/Info.plist',
                     typecode='BRPL', creator='????',
                     resources=['English.lproj', rsrc[config.name]])

Return('ret')
